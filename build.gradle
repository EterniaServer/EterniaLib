plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "7.1.0"
    id "com.adarshr.test-logger" version "3.2.0"
}

group = "br.com.eterniaserver"
version = "4.0-ALPHA"

repositories {
    mavenCentral()
    maven {
        name = "jitpack"
        url "https://jitpack.io"
    }
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "mojang"
        url = "https://libraries.minecraft.net"
    }
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.19.2-R0.1-SNAPSHOT")
    implementation("com.mojang:brigadier:1.0.18")
    implementation("com.zaxxer:HikariCP:5.0.1") {
        exclude group: "org.slf4j", module: "slf4j-api"
    }
    testImplementation("org.mariadb.jdbc:mariadb-java-client:3.0.7")
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.9.0")
    testImplementation("org.mockito:mockito-core:4.7.0")
    testImplementation("com.github.MockBukkit:MockBukkit:v1.19-SNAPSHOT")
}


shadowJar {
    relocate "com.mojang.brigadier", "br.com.eterniaserver.eternialib.brigadier"
    relocate "com.zaxxer.hikari", "br.com.eterniaserver.hikari"
    relocate "org.hibernate.Hibernate", "br.com.eterniaserver.Hibernate"
}

testlogger {
    theme "standard"
    showExceptions true
    showStackTraces true
    showFullStackTraces false
    showCauses true
    slowThreshold 2000
    showSummary true
    showSimpleNames false
    showPassed true
    showSkipped true
    showFailed true
    showOnlySlow false
    showStandardStreams false
    showPassedStandardStreams true
    showSkippedStandardStreams true
    showFailedStandardStreams true
    logLevel "lifecycle"
}

test {
    useJUnitPlatform()

    dependsOn "cleanTest"

    testLogging {
        events "passed", "skipped", "failed"
    }
}

tasks.build.dependsOn(shadowJar)

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset "UTF-8"
    filesMatching("plugin.yml") {
        expand props
    }
}
